#!/usr/bin/env perl6
use Net::Minecraft::Query;
use Terminal::ANSIColor;

sub MAIN(Str $host = 'localhost', UInt $port where uint16.Range = 25565) {

    say "Server Info for {colored(“$host:$port”, 'bold')}";

    my $info = Net::Minecraft::Query.ping(:$host, :$port);
    my @formats =
        # 1.8 uses a string for description, 1.9+ uses a hash
        description => {colored(.<text> // $_, 'bold blue')},
        players     => {sprintf('%d/%d', .<online max>)},
        version     => {sprintf('%s (proto %s)', .<name protocol>)},
        favicon     => {sprintf('(base64[%d bytes])', .chars * 0.8)},
        ping        => {colored($_ ~ 'ms', <green yellow>[$_ / 250] // 'red')},
    ;

    for @formats {
        with $info{.key}:delete -> $key {
            say .key.tc => .value.($key);
        }
    }

    if $info.keys -> $_ {
        note "Unhandled response keys: $_";
    }
}
