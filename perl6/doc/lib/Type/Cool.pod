=begin pod

=TITLE Cool

=SUBTITLE Value that can be treated as a string or number interchangeably

    class Cool is Any { }

C<Cool>, also known as the B<C>onvenient B<OO> B<L>oop, is a base class
for strings, numbers and other built-in classes that you are supposed to use
mostly interchangeably.

Methods in C<Cool> coerce the invocant to a more
specific type, and then call the same method on that type. For example both
L<Int> and L<Str> inherit from C<Cool>, and calling method C<substr> on an
C<Int> converts the integer to C<Str> first.

    123.substr(1, 1);   # '2', same as 123.Str.substr(1, 1)

The following built-in types inherit from C<Cool>:
L<Array> L<Backtrace> L<Bag> L<Baggy> L<Bool> L<Complex> L<Cool>
L<Duration> L<Enumeration> L<EnumMap> L<FatRat> L<Hash> L<Instant>
L<Int> L<Iterable> L<Iterator> L<KeyHash> L<KeySet> L<List>
L<ListIter> L<LoL> L<MapIter> L<Nil> L<Num> L<Numeric> L<Parcel>
L<Range> L<Real> L<Seq> L<Set> L<Stash> L<Str> L<Stringy>

The following table summarizes the methods that C<Cool> provides, and
what type they coerce to:

=begin table

    method          coercion type

    abs             Numeric
    conj            Numeric
    sqrt            Numeric
    sign            Real
    rand            Numeric
    sin             Numeric
    asin            Numeric
    cos             Numeric
    acos            Numeric
    tan             Numeric
    atan            Numeric
    atan2           Numeric
    sec             Numeric
    asec            Numeric
    cosec           Numeric
    acosec          Numeric
    cotan           Numeric
    acotan          Numeric
    sinh            Numeric
    asinh           Numeric
    cosh            Numeric
    acosh           Numeric
    sech            Numeric
    asech           Numeric
    asech           Numeric
    cosech          Numeric
    acotanh         Numeric
    cis             Numeric
    log             Numeric
    exp             Numeric
    roots           Numeric
    log10           Numeric
    unpolar         Numeric
    round           Numeric
    floor           Numeric
    ceiling         Numeric
    truncate        Numeric
    chr             Int
    ord             Str
    chars           Str
    fmt             Str
    uc              Str
    lc              Str
    capitalize      Str
    fc              Str
    flip            Str
    trans           Str
    index           Str
    rindex          Str
    ords            Str
    split           Str
    match           Str
    comb            Str
    subst           Str
    sprintf         Str
    printf          Str
    samecase        Str
    trim            Str
    trim-leading    Str
    trim-trailing   Str
    EVAL            Str

=end table

=head1 Methods

=head2 routine abs

    method abs()
    sub abs(Numeric() $x)

Coerces the invocant (or in the sub form, the argument) to
L<Numeric|/type/Numeric> and returns the absolute value (that is, a
non-negative number).

    say (-2).abs;       # 2
    say abs "6+8i";     # 10

=head2 method conj

    method conj()

Coerces the invocant to L<Numeric|/type/Numeric> and returns the
L<complex|/type/Complex> conjugate (that is, the number with the sign of the
imaginary part negated).

    say (1+2i).conj;        # 1-2i

=head2 routine sqrt

    method sqrt()
    sub sqrt(Numeric(Cool) $x)

Coerces the invocant to L<Numeric|/type/Numeric> (or in the sub form, the
arguemnt) and returns the square root,
that is, a non-negative number that, when multiplied with itself, produces the
original number.

    say 4.sqrt;             # 2
    say sqrt(2);            # 1.4142135623731

=head2 method sign

    method sign()

Coerces the invocant to L<Numeric|/type/Real> and returns its sign, that
is, 0 if the number is 0, 1 for positive and -1 for negative values.

    say 6.sign;             # 1
    say (-6).sign;          # -1
    say "0".sign;           # 0

=head2 method rand

    method rand()

Coerces the invocant to L<Num|/type/Num> and returns a pseudo-random value
between zero and the number.

    say 1e5.rand;           # 33128.495184283

=head2 method sin

    method sin()

Coerces the invocant to L<Numeric|/type/Numeric>, interprets it as radians,
returns its L<sine|https://en.wikipedia.org/wiki/Sine>.

    say sin(0);             # 0
    say sin(pi/4);          # 0.707106781186547
    say sin(pi/2);          # 1

Note that Perl 6 is no computer algebra system, so C<sin(pi)> typically does
not produce an exact 0, but rather a very small L<floating-point
number|/type/Num>.

=head2 method asin

    method asin()

Coerces the invocant to L<Numeric|/type/Numeric>, and returns its
L<arc-sine|https://en.wikipedia.org/wiki/Inverse_trigonometric_functions> in
radians.

=head2 method cos

    method cos()

Coerces the invocant to L<Numeric|/type/Numeric>, interprets it as radians,
returns its L<sine|https://en.wikipedia.org/wiki/Cosine>.

=head2 method acos

    method acos()

Coerces the invocant to L<Numeric|/type/Numeric>, and returns its
L<arc-cosine|https://en.wikipedia.org/wiki/Inverse_trigonometric_functions> in
radians.

=head2 method tan

Coerces the invocant to L<Numeric|/type/Numeric>, interprets it as radians,
returns its L<tangens|https://en.wikipedia.org/wiki/Tangent>.

=head2 method atan

    method atan()

Coerces the invocant to L<Numeric|/type/Numeric>, and returns its
L<arc-tangens|https://en.wikipedia.org/wiki/Inverse_trigonometric_functions> in
radians.

=head2 method atan2

    method atan2($y = 1e0)

Coerces the invocant to L<Numeric|/type/Numeric>, and together with its
argument, returns its two-argument
L<arc-tangens|https://en.wikipedia.org/wiki/Inverse_trigonometric_functions> in
radians.

=head2 method sec

    method sec()

Coerces the invocant to L<Numeric|/type/Numeric>, interprets it as radians,
returns its L<secans|https://en.wikipedia.org/wiki/Trigonometric_functions#Reciprocal_functions>,
that is, the reciprocal of its cosine.

=head2 method asec

    method asec()

Coerces the invocant to L<Numeric|/type/Numeric>, and returns its
L<arc-secans|https://en.wikipedia.org/wiki/Inverse_trigonometric_functions> in
radians.

=head2 method cosec

    method cosec()

Coerces the invocant to L<Numeric|/type/Numeric>, interprets it as radians,
returns its L<cosecans|https://en.wikipedia.org/wiki/Trigonometric_functions#Reciprocal_functions>,
that is, the reciprocal of its sine.

=head2 method acosec

    method acosec()

Coerces the invocant to L<Numeric|/type/Numeric>, and returns its
L<arc-cosecans|https://en.wikipedia.org/wiki/Inverse_trigonometric_functions> in
radians.

=head2 method cotan

    method cotan()

Coerces the invocant to L<Numeric|/type/Numeric>, interprets it as radians,
returns its L<cotangens|https://en.wikipedia.org/wiki/Trigonometric_functions#Reciprocal_functions>,
that is, the reciprocal of its tangens.

=head2 method acotan

    method acotan()

Coerces the invocant to L<Numeric|/type/Numeric>, and returns its
L<arc-cotangens|https://en.wikipedia.org/wiki/Inverse_trigonometric_functions> in
radians.

=head2 method sinh

    method sinh()

Coerces the invocant to L<Numeric|/type/Numeric>, and returns its
L<Sine hyperbolicus|https://en.wikipedia.org/wiki/Hyperbolic_function>.

=head2 method asinh

    method asinh()

Coerces the invocant to L<Numeric|/type/Numeric>, and returns its
L<Inverse Sine hyperbolicus|https://en.wikipedia.org/wiki/Inverse_hyperbolic_function>.

=head2 method cosh

    method cosh()

Coerces the invocant to L<Numeric|/type/Numeric>, and returns its
L<Cosine hyperbolicus|https://en.wikipedia.org/wiki/Hyperbolic_function>.

=head2 method acosh

    method acosh()

Coerces the invocant to L<Numeric|/type/Numeric>, and returns its
L<Inverse Cosine hyperbolicus|https://en.wikipedia.org/wiki/Inverse_hyperbolic_function>.

=head2 method tanh

    method tanh()

Coerces the invocant to L<Numeric|/type/Numeric>, and returns its
L<Tangens hyperbolicus|https://en.wikipedia.org/wiki/Hyperbolic_function>.

=head2 method atanh

    method atanh()

Coerces the invocant to L<Numeric|/type/Numeric>, and returns its
L<Inverse tangens hyperbolicus|https://en.wikipedia.org/wiki/Inverse_hyperbolic_function>.

=head2 method log

    multi method log(Cool:D: Cool:D $base?)

Coerces the invocant to L<Numeric|/type/Numeric>, and returns its
L<Logarithm|https://en.wikipedia.org/wiki/Logarithm> to base C<$base>, or to
base C<e> (Euler's Number) if no base was supplied
(L<Natural logarithm|https://en.wikipedia.org/wiki/Natural_logarithm>.

=head2 method exp

    multi method exp(Cool:D: Cool:D $base?)

Coerces the invocant to L<Numeric|/type/Numeric>, and returns C<$base>
raised to the power of this number. If no C<$base> is supplied, C<e> (Euler's
Number) is used.

    say 0.exp;      # 1
    say 1.exp;      # 2.71828182845905
    say 10.exp;     # 22026.4657948067

=head2 method round

    multi method round(Cool:D: $unit = 1)

Coerces the invocant to L<Numeric|/type/Numeric>, and rounds it to the unit of
C<$unit>. If C<$unit> is 1, rounds to the nearest integer.

    say 1.7.round;          # 2
    say 1.07.round(0.1);    # 1.1
    say 21.round(10);       # 20

=head2 method floor

    multi method floor

Coerces the invocant to L<Numeric|/type/Numeric>, and rounds it downwards to
the nearest integer.

    say "1.99".floor;       # 1
    say "-1.9".floor;       # -2
    say 0.floor;            # 0

=head2 method ceiling

    multi method ceiling

Coerces the invocant to L<Numeric|/type/Numeric>, and rounds it upwards to
the nearest integer.

    say "1".ceiling;        # 1
    say "-0.9".ceiling;     # 0
    say "42.1".ceiling;     # 43


=head2 method ord

    method ord()

Coerces the invocant to L<Str|/type/Str>, and returns the
L<Unicode code point|https://en.wikipedia.org/wiki/Code_point>,
number of the code point.

    say 'a'.ord;            # 65

The inverse operation is L<chr|#method chr>.

=head2 method chr

    method chr()

Coerces the invocant to L<Int|/type/Int>, interprets it as a
L<Unicode code points|https://en.wikipedia.org/wiki/Code_point>,
and returns a L<string|/type/Str> made of that code point.

    say '65'.chr;       # A

The inverse operation is L<ord|#method ord>.

Mnemonic: turns an integer into a I<char>acter.

=head2 method chars

    method chars()

Coerces the invocant to L<Str|/type/Str>, and returns the number of
characters in the string. Characters should actually be grapheme clusters,
though current implementation errornously count codepoints instead.

    say 'møp'.chars;    # 3

=head2 method codes

    method codes()

Coerces the invocant to L<Str|/type/Str>, and returns the number of
L<Unicode code points|https://en.wikipedia.org/wiki/Code_point>.

    say 'møp'.codes;    # 3

=head2 method flip

    method flip()

Coerces the invocant to L<Str|/type/Str>, and returns a reversed version.

    say 421.flip;       # 124

=head2 method trim

    method trim()

Coerces the invocant to L<Str|/type/Str>, and returns the string with both
leading and trailing whitespace stripped.

    my $stripped = '  abc '.trim;
    say "<$stripped>";          # <abc>

=head2 method trim-leading

    method trim(-leading)

Coerces the invocant to L<Str|/type/Str>, and returns the string with
leading whitespace stripped.

    my $stripped = '  abc '.trim-leading;
    say "<$stripped>";          # <abc >

=head2 method trim-trailing

    method trim-trailing()

Coerces the invocant to L<Str|/type/Str>, and returns the string with both
leading and trailing whitespace stripped.

    my $stripped = '  abc '.trim-trailing;
    say "<$stripped>";          # <  abc>

=head2 method lc

    method lc()

Coerces the invocant to L<Str|/type/Str>, and returns it case-folded to lower
case.

    say "ABC".lc;       # abc

=head2 method uc

    method uc()

Coerces the invocant to L<Str|/type/Str>, and returns it case-folded to upper
case (capital letters).

    say "Abc".uc;       # ABC

=head2 method tc

    method tc()

Coerces the invocant to L<Str|/type/Str>, and returns it with the first letter
case-folded to title case (or where not available, upper case).

    say "abC".tc;       # AbC

=head2 method tclc

    method tclc()

Coerces the invocant to L<Str|/type/Str>, and returns it with the first letter
case-folded to title case (or where not available, upper case), and the rest
of the string case-folded to lower case..

    say 'abC'.tclc;     # Abc

=head2 method wordcase

    method wordcase(:&filter = &tclc, Mu :$where = True)

Coerces the invocant to L<Str|/type/Str>, and filters each word that
smart-matches against C<$where> through the C<&filter>. With the default
filter (first character to upper case, rest to lower) and matcher (which
accepts everything), this title-cases each word:

    say "perl 6 programming".wordcase;      # Perl 6 Programming

With a mather:

    say "have fun working on perl".wordcase(:where({ .chars > 3 }));
                                            # Have fun Working on Perl

With a customer filter too:

    say "have fun working on perl".wordcase(:filter(&uc), :where({ .chars > 3 }));
                                            # HAVE fun WORKING on PERL

=head2 method chop

    method chop()

Coerces the invocant to L<Str|/type/Str>, and returns it with the last
character removed.

    say 'perl'.chop;                        # per

=head2 method chomp

    method chomp()

Coerces the invocant to L<Str|/type/Str>, and returns it with the last
character removed, if it is a logical newline.

    say 'ab'.chomp.chars;                   # 2
    say "a\n".chomp.chars;                  # 1

=head2 routine substr

    method substr($from, $chars?)
    sub substr(Str(Cool) $str, $from, $chars?)

Coerces the invocant (or in the sub form, the first argument) to
L<Str|/type/Str>, and returns the string starting from offset C<$from>. If
C<$chars> is supplied, at most C<$chars> characters are returned.

    say 'zenith'.substr(2);         # nith
    say 'zenith'.substr(0, 3);      # zen

    # works on non-strings too:
    say 20151224.substr(6);         # 24

    # sub form:
    say substr "zenith", 0, 3;      # zen

If the C<$from> parameter is a L<Callable|/type/Callable>, it is called with
the number of chars in the string as argument. This allows easy indexing
relative to the end:

    say 20151224.substr(*-2);       # 24

=head2 routine ords

    method ords()
    sub ords(Str(Cool) $str)

Coerces the invocant (or in the sub form, the first argument) to
L<Str|/type/Str>, and returns a list of Unicode codepoints for each character.

    say "Perl 6".ords;              # 80 101 114 108 32 54
    say ords 10;                    # 49 48

This is the list-returning version of L<ord>. The inverse operation in
L<chrs>.

=head2 routine chrs

    method chrs()
    sub chrs(*@codepoints) return Str:D

Coerces the invocant (or in the sub form, the argument list) to a list of
integers, and returns the string created by interpreting each integer as a
Unicode codepoint, and joining the characters.

    say <80 101 114 108 32 54>.chrs;    # Perl 6

This is the list-input version of L<chr>. The inverse operation is L<ords>.

=head2 routine split

    multi method split(  Str:D $delimiter, $limit = Inf, :$all)
    multi method split(Regex:D $delimiter, $limit = Inf, :$all)
    multi sub    split(  Str:D $delimiter, Str(Cool) $input, $limit = Inf, :$all)
    multi sub    split(Regex:D $delimiter, Str(Cool) $input, $limit = Inf, :$all)

Coerces the invocant (or in the sub form, the second argument) to
L<Str|/type/Str>, and splits it into pieces based on delimiters found in the
string.

If C<$delimiter> is a string, it is searched for literally and not treated
as a regex.

If the named parameter C<:all> is passed, the matches from C<$delimiter>
are included in the result list.

Note that unlike in Perl 5, empty chunks are not removed from the result list.
If you want that behavior, consider using L<comb> instead.

    say split(';', "a;b;c").perl;          # ("a", "b", "c").list
    say split(';', "a;b;c", :all).perl;    # ("a", ";", "b", ";", "c").list
    say split(';', "a;b;c", 2).perl;       # ("a", "b;c").list
    say split(';', "a;b;c", 2, :all).perl; #("a", ";", "b;c").list

    say split(';', "a;b;c,d").perl;        # ("a", "b", "c,d").list
    say split(/\;/, "a;b;c,d").perl;       # ("a", "b", "c,d").list
    say split(/<[;,]>/, "a;b;c,d").perl;   # ("a", "b", "c", "d").list

=head2 method words

    method words(Int() $limit)

Coerces the invocan to L<Str|/type/Str>, and returns a list of words that make
up the string (and if C<$limit> is supplied, only the first C<$limit> words).

    say 'The quick brown fox'.words.join('|');      # The|quick|brown|fox
    say 'The quick brown fox'.words(2).join('|');   # The|quick

Only whitespace counts as word boundaries

    say "isn't, can't".words.join('|');             # isn't,|can't


=head2 method IO

    method IO() returns IO::Path:D

Coerces the invocant to L<IO::Path>.

    .say for '.'.IO.dir;        # gives a directory listing

=begin comment

TODO: sech, asech, cosech, acosech, cotanh, acotanh, cis, roots, log10,
unpolar, truncate, fmt, substr-rw,
trans, index, rindex,
match, comb, lines, subst, sprintf, printf, samecase,
EVAL

TODO: subroutine forms (where they exist)

=end comment

=end pod
