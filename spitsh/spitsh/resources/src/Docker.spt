class Docker { }
class DockerImg { }

constant $*docker-cli = {
    Docker.install-cli;
    Cmd<docker>;
}

constant $*docker = {
    unless Cmd<docker> {
        if File</var/run/docker.sock> {
            Docker.install-cli;
        } else {
            ${ sh -c (HTTP<https://get.docker.com>.get) };
        }
    }
    Cmd<docker>;
};


augment Docker {
    static method install-cli {
        unless Cmd<docker> {
            note 'installing docker-cli from build';
            constant $latest-v = HTTP<https://github.com/moby/moby/releases/latest>
                                 .redirect-url.${sed 's/.*\/v//'};
            note "latest docker build is $latest-v";
            constant HTTP $url = "https://get.docker.com/builds/Linux/x86_64/docker-{$latest-v}.tgz";
            note "getting build from $url";
            $url.get.${tar xz};
            note "installing to /usr/local/bin/docker";
            File<docker/docker>.move-to('/usr/local/bin/docker');
            File<docker>.remove;
        }
    }

    static method socket(-->File) { '/var/run/docker.sock' }

    static method ?hello-world {
        ${ $*docker run -i --rm hello-world >X };
    }

    static method anon-create($from --> Docker) {
        ${ $*docker create -i $from sh } || die "couldn't create container from $from";
    }

    method ?exists { ${ $*docker container inspect $self *>X } }
    method Bool   { $self.exists }

    method ?create($from) {
        ${ $*docker create -i --name $self $from sh >X};
    }

    method copy($from,$to) {
        ${ $*docker cp $from "$self:$to" }
    }

    method ?remove { ${ $*docker rm -f $self >X} }

    method ?running {
        ${ $*docker inspect -f '{{.State.Status}}' $self } eq 'running';
    }

    method ?exec(Str $eval) {
        if $self.running {
            ${ $*docker exec -i $self sh -c $eval }
        } else {
            $eval.${ $*docker start -i $self };
        }
    }

    method commit(
        $name,
        :$tag,
        :$cmd,
        :$env,
        :$entrypoint,
        :$expose,
        :$label,
        :$onbuild,
        :$user,
        :$volume,
        :$workdir
        -->DockerImg) {
        my DockerImg $img-name = $name ~ (":$_" if $tag);
        ${$*docker commit
          ("-c=CMD $_" if $cmd )
          ("-c=ENV $_" if $env )
          ("-c=ENTRYPOINT $_" if $entrypoint)
          ("-c=EXPOSE $_" if $expose)
          ("-c=USER $_" if $user)
          ("-c=VOLUME $_" if $volume)
          ("-c=WORKDIR $_" if $workdir)
          $self
          $img-name
         } && $img-name;
    }
}

augment DockerImg {
    method ?exists { ${ $*docker image inspect $self *>X } }
    method ?remove { ${ $*docker rmi $self >X} }

    method once-off($code) {
        $code.${ $*docker run -i --rm --entrypoint sh };
    }
}
