sub kill(PID *@pids, :$signal = 'TERM') {
    ${kill "-$signal" @pids} if @pids;
}

sub wait(PID *@pids) {
    ${wait @pids}
}

#| The PID class represents an integer process ID.
augment PID {
    #| In Bool context the PID returns the result of `.exists`
    method Bool { $self.exists }
    #| Returns true if the process exists on the system.
    method exists? on {
        Alpine ${ps -o 'pid' | awk -v "p=$self" '$1==p{f=1}END{exit !f}'}
        Any    ${ps -p $self >X}
    }
    #| Sends the process a signal. Returns true if the signal was successfully
    #| sent.
    method kill($signal = 'TERM')? {
        ${kill "-$signal" $self};
    }

    method kill-group($signal = 'TERM')?{
        ${kill $signal "-$self"}
    }

    method children -->List[PID] {
        ${ps -o 'ppid,pid' | awk -v "p=$self" '$1==p{print $2}'}
    }

    method descendants-->List[PID] {
        $_, .descendants for $self.children;
    }

    method wait { wait $self }
}

augment List[PID] {
    method wait { wait @$self }
    method kill($signal = 'TERM') { kill :$signal, @$self }
}
